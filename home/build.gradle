if (HOME_MODULE_IS_APP.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion COMPILE_SDK_VERSION.toInteger()
    buildToolsVersion BUILD_TOOLS_VERSION

    if (!HOME_MODULE_IS_APP.toBoolean()) {
        resourcePrefix "${project.name}_" //给 Module 内的资源名增加前缀, 避免资源名冲突
    }

    defaultConfig {
        if (HOME_MODULE_IS_APP.toBoolean()) {
            applicationId "com.example.home"
        }

        minSdkVersion MIN_SDK_VERSION.toInteger()
        targetSdkVersion TARGET_SDK_VERSION.toInteger()
        versionCode VERSION_CODE.toInteger()
        versionName VERCODE_NAME

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [moduleName: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable = true
        }
    }

    sourceSets {
        main {
            if (HOME_MODULE_IS_APP.toBoolean()) {
                manifest.srcFile 'src/runalone/AndroidManifest.xml'
                java.srcDirs = ['src/main/java','src/runalone/java']
                res.srcDirs = ['src/main/res','src/runalone/res']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "androidx.appcompat:appcompat:$APPCOMPAT_VERSION"
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    annotationProcessor "com.alibaba:arouter-compiler:$AROUTER_COMPILER"
    implementation project(path: ':provider')

    if (HOME_MODULE_IS_APP.toBoolean()) {
        if (!MINE_MODULE_IS_APP.toBoolean()) {
            implementation project(path: ':mine')
        }
    }
}
